/* build with:
* make sure to use java 21 and groovy 4.0
*
* ./gradlew publishMyLibraryPublicationToMyRepository
*
* * publish manually at:
* https://central.sonatype.com/publishing
* or to publish locally
* ./gradlew publishToMavenLocal
*
* to update this wrapper version
* $ gradle wrapper --gradle-version 8.5 --distribution-type all
*
*/

buildscript {
    repositories {
        mavenCentral()
        maven {
           url = 'https://repo.grails.org/grails/restricted'
        }
    }
    dependencies { // Not Published to Gradle Plugin Portal
        classpath "cloud.wondrify:asset-pipeline-gradle"
        classpath platform("org.apache.grails:grails-bom:$grailsVersion")
        classpath "org.apache.grails:grails-data-hibernate5"
        classpath "org.apache.grails:grails-gradle-plugins"
    }
}

plugins {
    id "war"
    id "idea"
    id "eclipse"
    id 'maven-publish'
    id 'signing'
}

// Not Published to Gradle Plugin Portal
apply plugin: "org.apache.grails.gradle.grails-gsp"
apply plugin: "cloud.wondrify.asset-pipeline"
apply plugin: "org.apache.grails.gradle.grails-plugin"

version = "7.0.1-SNAPSHOT"
group = "net.codebuilders"
// archivesBaseName = "grails-paypal" // not needed
description = "A Grails plugin to integrate with Paypal and its Instant Payment Notification (IPN) system."

repositories {
    mavenCentral()
    maven {
       url = 'https://repo.grails.org/grails/restricted'
    }
}

dependencies {
    profile "org.apache.grails.profiles:web-plugin"
    developmentOnly "org.springframework.boot:spring-boot-devtools" // Spring Boot DevTools may cause performance slowdowns or compatibility issues on larger applications
    testAndDevelopmentOnly "org.webjars.npm:bootstrap"
    testAndDevelopmentOnly "org.webjars.npm:bootstrap-icons"
    testAndDevelopmentOnly "org.webjars.npm:jquery"
    implementation platform("org.apache.grails:grails-bom:$grailsVersion")
    implementation "org.apache.grails:grails-core"
    implementation "org.apache.grails:grails-data-hibernate5"
    implementation "org.apache.grails:grails-databinding"
    implementation "org.apache.grails:grails-events"
    implementation "org.apache.grails:grails-gsp"
    implementation "org.apache.grails:grails-interceptors"
    implementation "org.apache.grails:grails-layout"
    implementation "org.apache.grails:grails-logging"
    implementation "org.apache.grails:grails-rest-transforms"
    implementation "org.apache.grails:grails-scaffolding"
    implementation "org.apache.grails:grails-services"
    implementation "org.apache.grails:grails-url-mappings"
    implementation "org.apache.grails:grails-web-boot"
    implementation "org.springframework.boot:spring-boot-autoconfigure"
    implementation "org.springframework.boot:spring-boot-starter"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-logging"
    implementation "org.springframework.boot:spring-boot-starter-tomcat"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    console "org.apache.grails:grails-console"
    runtimeOnly "cloud.wondrify:asset-pipeline-grails"
    runtimeOnly "com.h2database:h2"
    runtimeOnly "com.zaxxer:HikariCP"
    runtimeOnly "org.fusesource.jansi:jansi"
    testImplementation "org.apache.grails:grails-testing-support-datamapping"
    testImplementation "org.apache.grails:grails-testing-support-web"
    testImplementation "org.spockframework:spock-core"
}

compileJava.options.release = 17

groovydoc {
    docTitle = "Grails Paypal"
    header = "Code Builders, LLC" // doesn't seem to work
    footer = "Generated documentation ${version}"
    windowTitle = docTitle
    use = true // create class and package usage pages
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(Test).configureEach {
    outputs.upToDateWhen { false } // always run tests
    useJUnitPlatform()
}

assets {
    packagePlugin = true
    excludes = [
            'webjars/jquery/**',
            'webjars/bootstrap/**',
            'webjars/bootstrap-icons/**'
    ]
    includes = [
            'webjars/jquery/*/dist/jquery.js',
            'webjars/bootstrap/*/dist/js/bootstrap.bundle.js',
            'webjars/bootstrap/*/dist/css/bootstrap.css',
            'webjars/bootstrap-icons/*/font/bootstrap-icons.css',
            'webjars/bootstrap-icons/*/font/fonts/*',
    ]
}

// publishing and signing
// guide: https://docs.gradle.org/current/userguide/publishing_setup.html

publishing {
    publications {
        myLibrary(MavenPublication) {
            artifactId = 'grails-paypal'
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'Grails Paypal Plugin'
                packaging = 'jar'
                description = 'A plugin for using Paypal IPN with Grails.'
                url = 'http://codebuilders.net'
                inceptionYear = '2025'

                scm {
                    url = 'https://github.com/cbmarcum/grails-paypal'
                    developerConnection = 'https://github.com/cbmarcum/grails-paypal'
                    connection = 'https://github.com/cbmarcum/grails-paypal'
                }

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        name = 'Carl Marcum'
                        email = 'carl.marcum@codebuilders.net'
                        url = 'https://codebuilders.net'
                        organization = 'Code Builders, LLC'
                        organizationUrl = 'https://codebuilders.net'

                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = 'myRepo'
            url = layout.buildDirectory.dir("repo")
        }
    }

}

signing {
    sign publishing.publications.myLibrary
}
